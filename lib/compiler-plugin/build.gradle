apply plugin: "java"

sourceSets {
	java9 {
		java {
			srcDirs = ["src/main/java9"]
		}
	}
}

java {
	modularity.inferModulePath = true
}

compileJava {
	sourceCompatibility "1.8"
	targetCompatibility "1.8"
	options.fork = true
	options.forkOptions.javaHome = file('/Library/Java/JavaVirtualMachines/jdk1.8.0_171.jdk/Contents/Home/')
}

compileJava9Java {
	sourceCompatibility "1.9"
	targetCompatibility "1.9"
	options.fork = true
	options.forkOptions.javaHome = file('/Library/Java/JavaVirtualMachines/jdk-9.jdk/Contents/Home/')
	options.compilerArgs = [
		"--add-exports",
		"jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED",
		"--add-exports",
		"jdk.compiler/com.sun.tools.javac.api=ALL-UNNAMED",
		"--add-exports",
		"jdk.compiler/com.sun.tools.javac.main=ALL-UNNAMED"
	]
}

jar {
	into("META-INF/versions/9") {
		from sourceSets.java9.output
	}
	manifest.attributes("Multi-Release": "true")
}

dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar'])
	java9Implementation files(sourceSets.main.output.classesDirs) { builtBy compileJava }
}